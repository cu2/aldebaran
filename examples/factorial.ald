# Print factorials from 0! to 8! forever
# 9! is out of word range
# FACT is a recursive function, parameters and return values are handled through the stack


# MAIN

START:
        MOV [COUNTER] 0000

LOOP:
        # AX = FACT([COUNTER]):
        DEC SP 0002
        PUSH [COUNTER]
        CALL FACT
        POP AX

        PRINT [COUNTER]
        PRINTCHAR 21  # !
        PRINTCHAR 3D  # =
        PRINT AX
        PRINTCHAR 20  # ' '
        INC [COUNTER] 0001
        JLE [COUNTER] [MAX_CNT] LOOP
        JMP START

# END MAIN


# SUB FACT (param): retval
FACT:
        # SUB PROLOGUE:
        ENTER 00
        PUSH AX  # save regs

        # SUBMAIN:
        JNZ [BP+05] NONZERO  # param == 0?
        MOV [BP+07] 0001  # retval = 1
        JMP EPILOGUE

NONZERO:
        SUB AX [BP+05] 0001  # AX = param - 1

        # AX = FACT(AX) = FACT(param - 1)
        DEC SP 0002
        PUSH AX
        CALL FACT
        POP AX

        MUL [BP+07] [BP+05] AX  # retval = param * FACT(param - 1)

EPILOGUE:
        POP AX  # restore regs
        LEAVE
        RETPOP 02

# ENDSUB FACT


# DATA

COUNTER:        DAT 00 00
MAX_CNT:        DAT 00 08

# END DATA
