# Print factorials from 0! to 8! forever
# 9! is out of word range
# FACT is a recursive function, parameters are handled through the stack, return value through registers


# MAIN
        .CONST $MAX_CNT 0x08

START:
        MOV [COUNTER]B 0x00

LOOP:
        # AX = FACT([COUNTER]B):
        PUSH [COUNTER]B
        CALL FACT

        PRINT [COUNTER]B
        PRINTCHAR 0x21  # !
        PRINTCHAR 0x3D  # =
        PRINT AX
        PRINTCHAR 0x20  # ' '
        INC [COUNTER]B 0x01
        JLE [COUNTER]B $MAX_CNT LOOP
        JMP START

# END MAIN


# SUB FACT (param): AX
FACT:
        # SUB PROLOGUE:
        ENTER 0x01 0x00
        .PARAMB $param

        # SUBMAIN:
        JNZ $param NONZERO  # param == 0?
        MOV AX 0x0001  # AX = 1
        JMP EPILOGUE

NONZERO:
        SUB AL $param 0x01  # AL = param - 1

        # AX = FACT(AL) = FACT(param - 1)
        PUSH AL
        CALL FACT

        MUL AX $param AX  # AX = param * FACT(param - 1)

EPILOGUE:
        LVRET

# ENDSUB FACT


# DATA

COUNTER:        .DAT 0x00

# END DATA
