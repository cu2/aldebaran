# Print factorials from 0! to 8! forever
# 9! is out of word range
# FACT is a recursive function, parameters are handled through the stack, return value through registers


# MAIN

START:
        MOV [COUNTER] 0x0000

LOOP:
        # AX = FACT([COUNTER]):
        PUSH [COUNTER]
        CALL FACT

        PRINT [COUNTER]
        PRINTCHAR 0x21  # !
        PRINTCHAR 0x3D  # =
        PRINT AX
        PRINTCHAR 0x20  # ' '
        INC [COUNTER] 0x0001
        JLE [COUNTER] [MAX_CNT] LOOP
        SHUTDOWN  # TODO: just for testing
        JMP START

# END MAIN


# SUB FACT (param): AX
FACT:
        # SUB PROLOGUE:
        ENTER 0x00

        # SUBMAIN:
        JNZ [BP+0x05] NONZERO  # param == 0?
        MOV AX 0x0001  # AX = 1
        JMP EPILOGUE

NONZERO:
        SUB AX [BP+0x05] 0x0001  # AX = param - 1

        # AX = FACT(AX) = FACT(param - 1)
        PUSH AX
        CALL FACT

        MUL AX [BP+0x05] AX  # AX = param * FACT(param - 1)

EPILOGUE:
        LEAVE
        RETPOP 0x02

# ENDSUB FACT


# DATA

COUNTER:        .DAT 0x0000
MAX_CNT:        .DAT 0x0008

# END DATA
