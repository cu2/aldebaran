# Print factorials from 0! to 8! forever
# 9! is out of word range
# FACT is a recursive function, parameters and return values are handled through the stack


# MAIN

START:
        MOV [COUNTER] 0x0000

LOOP:
        # AX = FACT([COUNTER]):
        DEC SP 0x0002
        PUSH [COUNTER]
        CALL FACT
        POP AX

        PRINT [COUNTER]
        PRINTCHAR 0x21  # !
        PRINTCHAR 0x3D  # =
        PRINT AX
        PRINTCHAR 0x20  # ' '
        INC [COUNTER] 0x0001
        JLE [COUNTER] [MAX_CNT] LOOP
        JMP START

# END MAIN


# SUB FACT (param): retval
FACT:
        # SUB PROLOGUE:
        ENTER 0x00
        PUSH AX  # save regs

        # SUBMAIN:
        JNZ [BP+0x05] NONZERO  # param == 0?
        MOV [BP+0x07] 0x0001  # retval = 1
        JMP EPILOGUE

NONZERO:
        SUB AX [BP+0x05] 0x0001  # AX = param - 1

        # AX = FACT(AX) = FACT(param - 1)
        DEC SP 0x0002
        PUSH AX
        CALL FACT
        POP AX

        MUL [BP+0x07] [BP+0x05] AX  # retval = param * FACT(param - 1)

EPILOGUE:
        POP AX  # restore regs
        LEAVE
        RETPOP 0x02

# ENDSUB FACT


# DATA

COUNTER:        .DAT 0x0000
MAX_CNT:        .DAT 0x0008

# END DATA
