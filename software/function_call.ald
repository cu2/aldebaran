# Generic function call with parameters, local variables and return value
# All caller-callee communication and local vars go on the stack


MAIN:
        MOV AX 1357  # for testing

        # retval = SUB(param1, param2)
        DEC SP 02  # 2 bytes for retval
        PUSH [PARAM1]
        PUSH [PARAM2]
        CALL SUB
        POP [RETVAL]

        PRINT [RETVAL]  # is it BCDE?
        PRINT AX  # is it still 1357?

        SHUTDOWN


SUB:
        # sub prologue:
        ENTER 06  # 6 bytes for local vars
        PUSH AX

        # sub main:
        MOV AX 0000
        PRINT [BP+05]  # param2
        PRINT [BP+07]  # param1
        PRINT [BP+09]  # retval
        MOV [BP-01] 2345  # var1 = 2345
        MOV [BP-03] 6789  # var2 = 6789
        MOV [BP-05] BCDE  # var3 = BCDE
        PRINT [BP-01]  # var1
        PRINT [BP-03]  # var2
        PRINT [BP-05]  # var3
        MOV [BP+09] [BP-05]  # retval = var3

        # sub epilogue:
        POP AX
        LEAVE
        RETPOP 04  # 4 bytes for params


PARAM1: DAT ABCD
PARAM2: DAT 1234
RETVAL: DAT 0000
